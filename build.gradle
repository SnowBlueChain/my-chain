buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.ajoberstar:grgit:1.7.2'
    }
}

plugins {
    id 'java'
}

ext {
    git = org.ajoberstar.grgit.Grgit.open()
    rev = git.head().id
    rev = git.head().getAbbreviatedId(8)
}

String versionEra = "4.11.05-${git.head().abbreviatedId}"
archivesBaseName = 'erachain-dev'
group 'org.erachain'
version versionEra
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url "https://artifacts.alfresco.com/nexus/content/repositories/public/"
    }
}

compileJava {
    options.encoding = "UTF-8"
}

jar {
    destinationDir project.file('build/libs/' + versionEra + '/bin')
    manifest {
        attributes('Main-Class': 'Start',
                'Implementation-Title': 'Gradle Build: ' + project.name,
                'Implementation-Version': project.version,
                'Build-Time': new Date().format("yyyy-MM-dd HH:mm:ssZ"),
                'Git-Commit': rev,
                'Git-Branch': git.branch.current.getName()
        )
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    exclude('META-INF/*.SF')
    exclude('META-INF/*.DSA')
    exclude('META-INF/*.RSA')
}

sourceSets {
    main {
        java {
            srcDir 'ERA/src'
        }
    }

    test {
        java {
            srcDir 'ERA/src/test'
        }
    }
}

build {
        finalizedBy = ["CopyResourse", "CreateZip"]
}

task CopyResourse {
    doFirst {
        copy {
            from 'ERA'
            into 'build/libs/' + versionEra + '/bin'
            include '*.bat'
            include '*.properties'
            include 'Erachain Licence*'
            include 'peers*'
        }
        copy {
            from 'ERA/web'
            into 'build/libs/' + versionEra + '/bin/web'
        }
        copy {
            from 'ERA/languages'
            into 'build/libs/' + versionEra + '/bin/languages'
        }
        copy {
            from 'ERA/themes'
            into 'build/libs/' + versionEra + '/bin/themes'
        }
        copy {
            from 'ERA/sounds'
            into 'build/libs/' + versionEra + '/bin/sounds'
        }
        copy {
            from 'ERA/images'
            into 'build/libs/' + versionEra + '/bin/images'
        }
    }
}
task CreateZip(type: Zip) {
    from 'build/libs/' + versionEra + '/bin'
    include '*'
    include '*/*'
    archiveName versionEra + '.zip'
    destinationDir(file('build/libs/' + versionEra))
}

dependencies {
    compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.54'
    compile group: 'commons-io', name: 'commons-io', version: '2.4'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
    compile group: 'commons-net', name: 'commons-net', version: '3.3'
    compile group: 'com.coverity.security', name: 'coverity-escapers', version: '1.1.1'
    compile group: 'com.googlecode.java-diff-utils', name: 'diffutils', version: '1.3.0'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.3.1'
    compile group: 'com.google.guava', name: 'guava', version: '19.0'
    compile group: 'com.toedter', name: 'jcalendar', version: '1.4'
    compile group: 'com.cedarsoftware', name: 'json-io', version: '4.8.0'
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    compile group: 'org.jsoup', name: 'jsoup', version: '1.8.2'
    compile group: 'org.mapdb', name: 'mapdb', version: '1.0.7'
    compile group: 'org.jvnet.mimepull', name: 'mimepull', version: '1.9.6'
    compile group: 'com.mitchellbosecke', name: 'pebble', version: '1.6.0'
    compile 'log4j:log4j:1.2.17'
    compile 'org.slf4j:slf4j-api:1.7.5'
    compile 'org.slf4j:slf4j-log4j12:1.7.5'
    compile group: 'com.twitter', name: 'twitter-text', version: '1.13.0'
    compile group: 'es.nitaur.markdown', name: 'txtmark', version: '0.16'
    compile group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.0.1'
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.0.1'
    compile group: 'javax.validation', name: 'validation-api', version: '1.1.0.Final'
    compile group: 'net.sf.tinylaf', name: 'tinylaf', version: '1.4.0'
    compile group: 'junit', name: 'junit', version: '4.12'
    compile group: 'org.glassfish.jersey.core', name: 'jersey-server', version: '2.22.2'
    compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet', version: '2.22.2'
    compile group: 'org.glassfish.jersey.media', name: 'jersey-media-multipart', version: '2.22.2'
    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.3.7.v20160115'
    compile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.3.7.v20160115'
    compile group: 'org.eclipse.jetty', name: 'jetty-util', version: '9.3.7.v20160115'
    compile group: 'net.sf.squirrel-sql.thirdparty-non-maven', name: 'tinylaf', version: '1.4.0'
    compile group: 'org.swinglabs', name: 'pdf-renderer', version: '1.0.5'
}